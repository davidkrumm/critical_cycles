/* 
This file contains MAGMA code used for calculations in Section 2.2. This includes:
 - Equations for the curves C_n (starting on line 9)
 - Proof of Proposition 2.3 (line 53)
 - Proof of Proposition 2.4 (line 101)
*/


//// *** Equations for the curves C_n *** ////

load "DynamicalModularCurves.m";

// Apply Proposition 2.2 to print an equation for C_n in Spec Q[r,s].

Cn_equation := procedure(n)
K<r,s> := PolynomialRing(Integers(),2);
R<x> := PolynomialRing(K);
phi_rs := (2*x^2 + (2 - r)*x + (2 - r))/(-x^2 + (2 + r)*x + 2 - r - s);
PHI := R ! DynatomicPolynomial(phi_rs,n);
assert Discriminant(PHI) ne 0;
lc := LeadingCoefficient(PHI);
phi_rs_der := Derivative(phi_rs);
p := Numerator(phi_rs_der);
q := Denominator(phi_rs_der);
Fn_numerator := Resultant(PHI,p)/lc^Degree(p);
Fn_denominator := Resultant(PHI,q)/lc^Degree(q);
Fn := ExactQuotient(Fn_numerator, Fn_denominator);
S := Parent(Fn_numerator);
AssignNames(~S, ["r", "s"]);
Sprint(Fn);
end procedure;

// Print equations for C_1, C_2, C_3, C_4.

for i in [1..4] do
Cn_equation(i);
end for;

/* 

Output:

r - 2
2*r + s
2*r^3 + 5*r^2*s - r^2 + 4*r*s^2 - 2*r*s + 12*r + s^3 + 28
2*r^5 + 4*r^4*s^2 - 3*r^4*s + 27*r^4 + 12*r^3*s^3 - 4*r^3*s^2 + 30*r^3*s + 
104*r^3 + 13*r^2*s^4 - 5*r^2*s^3 + 68*r^2*s^2 + 4*r^2*s + 296*r^2 + 6*r*s^5 - 
2*r*s^4 + 28*r*s^3 + 128*r*s^2 - 152*r*s + 640*r + s^6 + 60*s^3 - 48*s^2 + 96*s + 304

*/


//// *** Proof of Proposition 2.3 *** ////

// Construct a parametrization of the curve C_3.

A1<v> := AffineSpace(Rationals(),1);
A2<r,s> := AffineSpace(Rationals(),2);
F3 := 2*r^3 + 5*r^2*s - r^2 + 4*r*s^2 - 2*r*s + 12*r + s^3 + 28; 
C3 := Curve(A2,F3);
epsilon := -(v^3 - v^2 + 5*v + 1)/v;
rho := (v^3 - 2*v^2 + 7*v + 2)/v;
p := map<A1->C3|[epsilon,rho]>;
bool, inv := IsInvertible(p);
assert bool;

// Find points where the parametrization is undefined.
// Note that (-6, 8) = p(1) is in the image of p.

BasePoints(p); // {@ (0) @}
BasePoints(inv); // {@ (-6, 8) @}

// Check that no rational point on C3 maps to 0.

inv := ProjectiveClosure(inv);
Pullback(inv,Codomain(inv)![0]); // (-1/2 : 1 : 0)

// Construct a parametrization of the curve C_4.

F4 := 2*r^5 + 4*r^4*s^2 - 3*r^4*s + 27*r^4 + 12*r^3*s^3 - 4*r^3*s^2 + 30*r^3*s + 
104*r^3 + 13*r^2*s^4 - 5*r^2*s^3 + 68*r^2*s^2 + 4*r^2*s + 296*r^2 + 6*r*s^5 - 
2*r*s^4 + 28*r*s^3 + 128*r*s^2 - 152*r*s + 640*r + s^6 + 60*s^3 - 48*s^2 + 96*s + 304; 
C4 := Curve(A2,F4);
eta := -(v^6 + v^5 + 2*v^4 + 3*v^3 - 2*v^2 - 5*v - 1)/(v*(v - 1)*(v + 1)^2);
kappa := (v^5 + 2*v^4 + 3*v^3 - 5*v - 2)/(v*(v^2 - 1));
p := map<A1->C4|[eta,kappa]>;
bool, inv := IsInvertible(p);
assert bool;

// Find points where the parametrization is undefined.

BasePoints(p); // {@ (-1), (0), (1) @}
BasePoints(inv); // {@ @}

// Check that no rational point on C4 maps to -1, 0, or 1.

inv := ProjectiveClosure(inv);
{Pullback(inv,Codomain(inv)![i]) : i in [-1,0,1]}; // { (-1/2 : 1 : 0), (1 : 0 : 0) }


//// *** Proof of Proposition 2.4 *** ////

// Equations for the symmetry locus \mathcal{S} and for C_2, C_3, C_4.

A2<r,s> := AffineSpace(Rationals(),2);
sym := -2*r^3 - r^2*s + r^2 + 8*r*s + 4*s^2 - 12*r - 12*s + 36;
F2 := 2*r + s;
F3 := 2*r^3 + 5*r^2*s - r^2 + 4*r*s^2 - 2*r*s + 12*r + s^3 + 28;
F4 := 2*r^5 + 4*r^4*s^2 - 3*r^4*s + 27*r^4 + 12*r^3*s^3 - 4*r^3*s^2 + 30*r^3*s + 
104*r^3 + 13*r^2*s^4 - 5*r^2*s^3 + 68*r^2*s^2 + 4*r^2*s + 296*r^2 + 6*r*s^5 - 
2*r*s^4 + 28*r*s^3 + 128*r*s^2 - 152*r*s + 640*r + s^6 + 60*s^3 - 48*s^2 + 96*s 
+ 304;

/* Construct the intersection of C_2 with the symmetry locus,
and verify the intersection has only one algebraic point. */

C2_meet_S := Scheme(A2,[sym,F2]);
assert Dimension(C2_meet_S) eq 0;
assert not HasPointsOverExtension(C2_meet_S);
Points(C2_meet_S); // {@ (-6, 12) @}

/* Construct the intersection of C_3 with the symmetry locus S,
and find the field of definition of each of its algebraic points. */

C3_meet_S := Scheme(A2,[sym,F3]);
pts := PointsOverSplittingField(C3_meet_S);
pts;

/* The points returned are

{@ (3/128*r1^3 - 5/16*r1^2 + 9/8*r1 - 5/2, r1), 
   (3/128*r2^3 - 5/16*r2^2 + 9/8*r2 - 5/2, r2),
   (3/128*r3^3 - 5/16*r3^2 + 9/8*r3 - 5/2, r3), 
   (3/128*r4^3 - 5/16*r4^2 + 9/8*r4 - 5/2, r4) @}.

Note that the field of definition of each point
is the number field generated by the second coordinate.*/

// Find a defining polynomial for each field of definition.

for P in pts do
MinimalPolynomial(P[2]);
end for;

/*

Output:

$.1^4 - 16*$.1^3 + 112*$.1^2 - 320*$.1 + 512
$.1^4 - 16*$.1^3 + 112*$.1^2 - 320*$.1 + 512
$.1^4 - 16*$.1^3 + 112*$.1^2 - 320*$.1 + 512
$.1^4 - 16*$.1^3 + 112*$.1^2 - 320*$.1 + 512

*/
 

/* Construct the intersection of C_4 with the symmetry locus S,
and find the field of definition of each of its algebraic points. */

C4_meet_S := Scheme(A2,[sym,F4]);
pts := PointsOverSplittingField(C4_meet_S);
pts;

/* The points returned are

{@ (155/13212*r1^5 - 100/367*r1^4 + 30371/13212*r1^3 - 28415/3303*r1^2 + 56098/3303*r1 - 62786/3303, r1),
   (155/13212*r2^5 - 100/367*r2^4 + 30371/13212*r2^3 - 28415/3303*r2^2 + 56098/3303*r2 - 62786/3303, r2), 
   (155/13212*r3^5 - 100/367*r3^4 + 30371/13212*r3^3 - 28415/3303*r3^2 + 56098/3303*r3 - 62786/3303, r3),
   (155/13212*r4^5 - 100/367*r4^4 + 30371/13212*r4^3 - 28415/3303*r4^2 + 56098/3303*r4 - 62786/3303, r4), 
   (155/13212*r5^5 - 100/367*r5^4 + 30371/13212*r5^3 - 28415/3303*r5^2 + 56098/3303*r5 - 62786/3303, r5),
   (155/13212*r6^5 - 100/367*r6^4 + 30371/13212*r6^3 - 28415/3303*r6^2 + 56098/3303*r6 - 62786/3303, r6),
   (-2*r7 + 2, r7), 
   (-2*r8 + 2, r8) @}.

Note that the field of definition of each point
is the number field generated by the second coordinate.*/

// Find a defining polynomial for each field of definition.

for P in pts do
MinimalPolynomial(P[2]);
end for;

/*

Output:

$.1^6 - 26*$.1^5 + 259*$.1^4 - 1248*$.1^3 + 3328*$.1^2 - 5312*$.1 + 4352
$.1^6 - 26*$.1^5 + 259*$.1^4 - 1248*$.1^3 + 3328*$.1^2 - 5312*$.1 + 4352
$.1^6 - 26*$.1^5 + 259*$.1^4 - 1248*$.1^3 + 3328*$.1^2 - 5312*$.1 + 4352
$.1^6 - 26*$.1^5 + 259*$.1^4 - 1248*$.1^3 + 3328*$.1^2 - 5312*$.1 + 4352
$.1^6 - 26*$.1^5 + 259*$.1^4 - 1248*$.1^3 + 3328*$.1^2 - 5312*$.1 + 4352
$.1^6 - 26*$.1^5 + 259*$.1^4 - 1248*$.1^3 + 3328*$.1^2 - 5312*$.1 + 4352
$.1^2 - 4*$.1 + 16/3
$.1^2 - 4*$.1 + 16/3

*/
