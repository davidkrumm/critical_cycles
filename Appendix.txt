/* 
This file contains MAGMA code used for calculations in Appendix A. This includes:
 - Proof of Lemma A.1 (Lemma 3.4) (starting on line 7)
*/


//// *** Proof of Lemma A.1 *** ////

/* For c of the form (q^3 + 1)^2/q^3, the critical point alpha of psi_c is rational,
and c*alpha - 1 has a rational cube root. */

Qq<q> := FunctionField(Rationals());
c := (q^3 + 1)^2/q^3;
bool, g := IsSquare(c*(c - 4));
alpha := (c + g)/(2*c);
assert alpha eq q^3/(q^3 + 1);
assert c*alpha - 1 eq q^3;

// Parametrize the curve c*(c - 4) = x^2.

AA<c,x> := AffineSpace(Rationals(),2);
Y := Curve(AA,x^2 - c*(c - 4));
A1<s> := AffineSpace(Rationals(),1);
m := map<A1->Y|[(s + 1)^2/s,(s^2 - 1)/s]>;
bool, inv := IsInvertible(m);
BasePoints(m); // {@ (0) @}
BasePoints(inv); // {@ @}

// Suppose alpha is irrational and c*alpha - 1 has cube root p + q*alpha.

// Check p cannot be -1.

_<c> := PolynomialRing(Rationals());
p := -1;
q := 3;
poly := c^2 - (3*p^2*q + 3*p*q^2 + q^3)*c + q^3;
assert poly eq c^2 - 9*c +27;
Roots(poly); // []

// Check that c = (4*p^3 - 3*p + 1)/p^3 when 2*p^2 + p*q + p - 1 = 0.

Qp<p> := FunctionField(Rationals());
q := (1 - p - 2*p^2)/p;
c := (3*p*q^2 + q^3)/(p^3 + 1);
assert c eq (4*p^3 - 3*p + 1)/p^3;

/* Check that when c has the form (4*p^3 - 3*p + 1)/p^3,
we have p^6*c*(c - 4) = (p + 1)*(1 - 3*p)*(2*p - 1)^2,
and c*alpha - 1 has a cube root in Q(alpha). */

Qp<p> := FunctionField(Rationals());
c := (4*p^3 - 3*p + 1)/p^3;
assert p^6*c*(c - 4) eq (p + 1)*(1 - 3*p)*(2*p - 1)^2;
q := (1 - p - 2*p^2)/p;
_<x> := PolynomialRing(Qp);
K<g> := ext<Qp|x^2 - c*(c - 4)>;
alpha := (c + g)/(2*c);
assert (p + q*alpha)^3 eq c*alpha - 1;

// Check the equation for the (p,q)-curve.

Qpq<p,q> := FunctionField(Rationals(),2);
c := (3*p*q^2 + q^3)/(p^3 + 1);
// Substitute c in the equation c^2 - (3*p^2*q + 3*p*q^2 + q^3)*c + q^3 = 0.
f1 := 2*p^2 + p*q + p - 1;
f2 := p^2*q^2 + 4*p^3*q + 4*p^4 - p^2*q - 2*p^3 + 3*p^2 + p*q + p + 1;
assert -q^3*f1*f2/(p^3 + 1)^2 eq c^2 - (3*p^2*q + 3*p*q^2 + q^3)*c + q^3;

/* If we assume the RHS of asserted relation is 0, then LHS is 0.
Thus, an equation for the (p,q)-curve is f1*f2 = 0. */

// Show that the (p,q)-curve is irreducible but geometrically reducible.

AA<p,q> := AffineSpace(Rationals(),2);
C := Curve(AA,p^2*q^2 + 4*p^3*q + 4*p^4 - p^2*q - 2*p^3 + 3*p^2 + p*q + p + 1);
assert IsIrreducible(C);
assert not IsAbsolutelyIrreducible(C);

// Thus every rational point on the (p,q)-curve is singular.
SingularPoints(C); // {@ (-1, 3) @}
